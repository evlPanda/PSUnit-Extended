import TTS_UNITTEST:TestBase;
import TTS_UNITTEST:Exceptions:AssertFailed;

class Base extends TTS_UNITTEST:TestBase
   property string className;
   property string description;
   property any expectedResult;
   property any actualResult;
   property string pass get;
   property string fail get;
   method Base(&className_ As string);
   method setup();
   method teardown();
   method test();
   method doTest();
   method Assert(&isTrue As boolean, &onFail As string);
   method AssertStringsEqual(&str1 As string, &str2 As string, &onFail As string);
   method AssertNumbersEqual(&num1 As number, &num2 As number, &onFail As string);
   method getFailMessage() Returns string;
   method passTest();
   method failTest();
   method passClass();
   method tableRow(&1 As string, &2 As any, &3 As any, &4 As string);
end-class;

get pass
   /+ Returns String +/
   Return " <span style = ""color: #859900; font-weight: bold;"">PASS</span>";
end-get;

get fail
   /+ Returns String +/
   Return " <span style = ""color: #dc322f; font-weight: bold;"">FAIL</span>";
end-get;


method Base
   /+ &className_ as String +/
   %Super = create TTS_UNITTEST:TestBase(&className_);
   %This.className = &className_;
end-method;

method setup
   /+ Extends/implements TTS_UNITTEST:TestBase.Setup +/
   %This.HTMLMsg("<table width=""100%"" style=""color:#657b83; background-color: #fdf6e3; box-shadow: 3px 5px #ccc; border: 1px solid; padding: 1px;"">");
   %This.HTMLMsg("<tr><td colspan=4 align=left><span style=""font-weight: bold; color: #268bd2"">" | %This.className | "</span></td></tr>");
   %This.HTMLMsg("<tr><td width=""30%""><b>Description</b></td><td width=""30%""><b>Expected Result</b></td><td width=""30%""><b>Actual Result</b></td><td width=""10%""><b>Pass</b></td></tr>");
end-method;

method test
   %This.doTest();
   %This.passTest();
end-method;

method doTest
   Evaluate True
   When %This.expectedResult /* boolean */
   When ( Not %This.expectedResult)
      %This.Assert((%This.expectedResult = %This.actualResult), %This.getFailMessage());
      Return;
   When IsNumber(%This.expectedResult) /* number */
      %This.AssertNumbersEqual(%This.expectedResult, %This.actualResult, %This.getFailMessage());
      Return;
   When IsAlphaNumeric(%This.expectedResult) /* string */
   When IsAlpha(%This.expectedResult)
   When IsDigits(%This.expectedResult)
      %This.AssertStringsEqual(%This.expectedResult, %This.actualResult, %This.getFailMessage());
      Return;
   End-Evaluate;
end-method;

method Assert
   /+ &isTrue as Boolean, +/
   /+ &onFail as String +/
   /+ Extends/implements TTS_UNITTEST:TestBase.Assert +/
   If &isTrue Then
      Return;
   End-If;
   %This.failTest();
   throw create TTS_UNITTEST:Exceptions:AssertFailed(&onFail);
end-method;

method AssertStringsEqual
   /+ &str1 as String, +/
   /+ &str2 as String, +/
   /+ &onFail as String +/
   /+ Extends/implements TTS_UNITTEST:TestBase.AssertStringsEqual +/
   If &str1 = &str2 Then
      Return;
   End-If;
   %This.failTest();
   Local string &msg = &str1 | " <> " | &str2 | "! " | &onFail;
   throw create TTS_UNITTEST:Exceptions:AssertFailed(&msg);
end-method;

method AssertNumbersEqual
   /+ &num1 as Number, +/
   /+ &num2 as Number, +/
   /+ &onFail as String +/
   /+ Extends/implements TTS_UNITTEST:TestBase.AssertNumbersEqual +/
   If &num1 = &num2 Then
      Return;
   End-If;
   %This.failTest();
   Local string &msg = String(&num1) | " <> " | String(&num2) | "! " | &onFail;
   throw create TTS_UNITTEST:Exceptions:AssertFailed(&msg);
end-method;


method getFailMessage
   /+ Returns String +/
   Return "Expected: '" | %This.expectedResult | "', Actual: '" | %This.actualResult | "'";
end-method;

method passTest
   %This.tableRow(%This.description, %This.expectedResult, %This.actualResult, %This.pass);
end-method;

method failTest
   %This.tableRow(%This.description, %This.expectedResult, %This.actualResult, %This.fail);
end-method;

method passClass
   %This.HTMLMsg("<tr><td colspan=3 align=left><b>" | %This.className | "</b></td><td><b>" | %This.pass | "<b></td></tr>");
end-method;

method tableRow
   /+ &1 as String, +/
   /+ &2 as Any, +/
   /+ &3 as Any, +/
   /+ &4 as String +/
   %This.HTMLMsg("<tr><td>" | &1 | "</td><td>" | &2 | "</td><td>" | &3 | "</td><td>" | &4 | "</td></tr>");
end-method;

method teardown
   /+ Extends/implements TTS_UNITTEST:TestBase.Teardown +/
   %This.HTMLMsg("</table><br>");
end-method;
